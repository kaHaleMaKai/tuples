/*
Tuples â€“ a type-safe tuple and tuple list implementation
Copyright (C) 2016  Lars Winderling

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
*/

plugins {
    id "com.jfrog.bintray" version "1.6"
}

group 'com.github.kahalemakai'
version '0.4.0'
def license = "GPL 3.0"
def vcsUrl = 'https://github.com/kaHaleMaKai/tuples.git'
description """Type-safe tuples and tuple list"""
def delombokedDir = "${buildDir}/src-delomboked"
def lombokDependency = 'org.projectlombok:lombok:1.16.12'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compileOnly lombokDependency
    testCompile 'junit:junit:4.12'
}

configurations {
    lombok
    lombok.extendsFrom configurations.compile
    dependencies {
        compile lombokDependency
    }
}

task delombok {
    doLast {
        description 'Delomboks the source code'
        ant.taskdef(classname: 'lombok.delombok.ant.Tasks$Delombok', classpath: configurations.lombok.asPath, name: 'delombok')
        ant.mkdir(dir: delombokedDir)
        ant.delombok(verbose: 'true', encoding: 'UTF-8', to: delombokedDir, from: 'src/main/java')
    }
}

task javadoc(type: Javadoc, group: "documentation", overwrite: true) {
    dependsOn delombok
    source delombokedDir
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
    dependsOn javadoc
}

task sourcesJar(type: Jar) {
    dependsOn classes
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        tuples(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar
            groupId rootProject.group
            artifactId rootProject.name
            version rootProject.version
        }
    }
}

bintray {
    user = rootProject.hasProperty("bintrayUser") ? bintrayUser : ""
    key = rootProject.hasProperty("bintrayApiKey") ? bintrayApiKey : ""
    publications = ["tuples"]
    pkg {
        repo = 'maven'
        name = rootProject.name
        licenses = [license]
        vcsUrl = vcsUrl

        githubRepo = 'kaHaleMaKai/tuples'
        githubReleaseNotesFile = 'README.md'

        publicDownloadNumbers = true

        version {
            name = rootProject.version
            desc = "add javadocs"
            released = new Date()
            vcsTag = rootProject.version

            gpg {
                sign = true
                passphrase = hasProperty("gpgPassPhrase") ? gpgPassPhrase : ""
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

defaultTasks 'clean', 'test', 'build', 'jacocoTestReport', 'javadoc'
